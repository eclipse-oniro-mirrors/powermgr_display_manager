# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../../displaymgr.gni")

copy_taihe_idl("copy_display_manager") {
  sources = [ "idl/ohos.brightness.taihe" ]
}

subsystem_name = "powermgr"
part_name = "display_manager"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_display_manager" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.brightness.ani.cpp",
    "$taihe_generated_file_path/src/ohos.brightness.abi.c",
  ]
}

taihe_shared_library("display_manager_brightness_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "./include" ]
  configs = [
    "${displaymgr_utils_path}:utils_config",
    "${displaymgr_utils_path}:coverage_flags",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.brightness.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${displaymgr_inner_api}:displaymgr",
  ]
  external_deps = [
    "hilog:libhilog",
    "power_manager:powermgr_client",
  ]
}

generate_static_abc("brightness_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.brightness.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/brightness_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("brightness_etc") {
  source = "$target_out_dir/brightness_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":brightness_abc" ]
}