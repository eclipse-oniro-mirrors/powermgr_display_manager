# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("../../displaymgr.gni")

config("displaymgr_private_config") {
  include_dirs = [ "${displaymgr_inner_api}/native/include" ]
}

config("displaymgr_public_config") {
  include_dirs = [
    "native/include",
    "zidl/include",
    "${target_gen_dir}",
  ]
}

ohos_shared_library("displaymgrservice") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  sources = [
    "${displaymgr_utils_path}/native/src/display_xcollie.cpp",
    "native/src/display_auto_brightness.cpp",
    "native/src/display_common_event_mgr.cpp",
    "native/src/display_param_helper.cpp",
    "native/src/display_power_mgr_service.cpp",
    "native/src/display_setting_helper.cpp",
    "native/src/display_system_ability.cpp",
    "native/src/gradual_animator.cpp",
    "native/src/screen_action.cpp",
    "native/src/screen_controller.cpp",
    "zidl/src/display_brightness_callback_proxy.cpp",
    "zidl/src/display_power_callback_proxy.cpp",
  ]

  configs = [
    "${displaymgr_utils_path}:utils_config",
    ":displaymgr_private_config",
    "${displaymgr_utils_path}:coverage_flags",
  ]

  deps = [
    "${displaymgr_root_path}/service:displaymgr_stub",
  ]
  public_configs = [
    ":displaymgr_public_config",
    "${brightnessmgr_root_path}:brightness_manager_config",
  ]

  public_deps = [ "${brightnessmgr_root_path}:brightness_manager" ]

  external_deps = [ "power_manager:power_permission" ]
  external_deps += [
    "ability_base:zuri",
    "ability_runtime:ability_manager",
    "cJSON:cjson",
    "c_utils:utils",
    "data_share:datashare_consumer",
    "eventhandler:libeventhandler",
    "ffrt:libffrt",
    "graphic_2d:librender_service_base",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "image_framework:image_native",
    "ipc:ipc_core",
    "power_manager:power_ffrt",
    "power_manager:power_setting",
    "power_manager:power_sysparam",
    "power_manager:powermgr_client",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "skia:skia_canvaskit",
    "window_manager:libdm_lite",
  ]

  defines = []
  if (has_sensors_sensor_part) {
    external_deps += [ "sensor:sensor_interface_native" ]
    defines += [ "ENABLE_SENSOR_PART" ]
  }
  if (has_hiviewdfx_hisysevent_part) {
    external_deps += [ "hisysevent:libhisysevent" ]
  }
  if (use_clang_coverage) {
    defines += [ "FUZZ_COV_TEST" ]
  }
  shlib_type = "sa"
  subsystem_name = "powermgr"
  part_name = "${displaymgr_part_name}"
}

idl_gen_interface("displaymgr_interface") {
  sources = [
    "IDisplayPowerMgr.idl"
  ]
  configs = [
    "${displaymgr_utils_path}:utils_config",
    ":displaymgr_private_config",
    "${displaymgr_utils_path}:coverage_flags",
  ]
  log_domainid = "0xD002982"
  log_tag = "DisplayPowerSvc"
  part_name = "${displaymgr_part_name}"
  subsystem_name = "powermgr"
}

ohos_source_set("displaymgr_proxy") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":displaymgr_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])

  configs = [
    "${displaymgr_utils_path}:utils_config",
    ":displaymgr_private_config",
    "${displaymgr_utils_path}:coverage_flags",
  ]
  deps = [ ":displaymgr_interface" ]
  public_configs = [ ":displaymgr_public_config" ]

  external_deps = [
    "c_utils:utils",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "ipc:ipc_core",
    "power_manager:power_setting",
    "safwk:system_ability_fwk",
  ]
  part_name = "${displaymgr_part_name}"
  subsystem_name = "powermgr"
}

ohos_source_set("displaymgr_stub") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":displaymgr_interface")
  sources = filter_include(output_values, [ "*_stub.cpp" ])
  
  configs = [
    "${displaymgr_utils_path}:utils_config",
    ":displaymgr_private_config",
    "${displaymgr_utils_path}:coverage_flags",
  ]
  deps = [ ":displaymgr_interface" ]
  public_configs = [ ":displaymgr_public_config" ]

  external_deps = [
    "c_utils:utils",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "ipc:ipc_core",
    "power_manager:power_setting",
    "safwk:system_ability_fwk",
  ]

  subsystem_name = "powermgr"
  part_name = "${displaymgr_part_name}"
}
