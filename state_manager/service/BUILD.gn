# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../displaymgr.gni")

config("displaymgr_private_config") {
  include_dirs = [ "${displaymgr_inner_api}/native/include" ]
}

config("displaymgr_public_config") {
  include_dirs = [
    "native/include",
    "zidl/include",
  ]
}

ohos_shared_library("displaymgrservice") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  sources = [
    "native/src/display_auto_brightness.cpp",
    "native/src/display_param_helper.cpp",
    "native/src/display_power_mgr_service.cpp",
    "native/src/display_setting_helper.cpp",
    "native/src/display_system_ability.cpp",
    "native/src/gradual_animator.cpp",
    "native/src/screen_action.cpp",
    "native/src/screen_controller.cpp",
    "zidl/src/display_brightness_callback_proxy.cpp",
    "zidl/src/display_power_callback_proxy.cpp",
    "zidl/src/display_power_mgr_stub.cpp",
  ]

  configs = [
    "${displaymgr_utils_path}:utils_config",
    ":displaymgr_private_config",
    "${displaymgr_utils_path}:coverage_flags",
  ]

  public_configs = [
    ":displaymgr_public_config",
    "${brightnessmgr_root_path}:brightness_manager_config",
  ]

  public_deps = [ "${brightnessmgr_root_path}:brightness_manager" ]

  external_deps = [ "power_manager:power_permission" ]
  external_deps += [
    "ability_runtime:ability_manager",
    "c_utils:utils",
    "data_share:datashare_consumer",
    "ffrt:libffrt",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "image_framework:image_native",
    "ipc:ipc_core",
    "power_manager:power_ffrt",
    "power_manager:power_setting",
    "power_manager:power_sysparam",
    "power_manager:powermgr_client",
    "safwk:system_ability_fwk",
    "window_manager:libdm",
  ]

  defines = []
  if (has_sensors_sensor_part) {
    external_deps += [ "sensor:sensor_interface_native" ]
    defines += [ "ENABLE_SENSOR_PART" ]
  }

  shlib_type = "sa"
  subsystem_name = "powermgr"
  part_name = "${displaymgr_part_name}"
}

ohos_shared_library("displaymgr_stub") {
  sources = [ "zidl/src/display_power_mgr_stub.cpp" ]

  configs = [
    "${displaymgr_utils_path}:utils_config",
    ":displaymgr_private_config",
    "${displaymgr_utils_path}:coverage_flags",
  ]

  public_configs = [ ":displaymgr_public_config" ]

  external_deps = [
    "c_utils:utils",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "ipc:ipc_core",
    "power_manager:power_setting",
    "safwk:system_ability_fwk",
  ]

  subsystem_name = "powermgr"
  part_name = "${displaymgr_part_name}"
}
